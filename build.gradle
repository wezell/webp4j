apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

compileJava.options.encoding = 'UTF-8'

group = 'com.github.zakgof'
archivesBaseName = 'webp4j'
version = '0.0.1'

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.scijava:native-lib-loader:2.3.2'
    testCompile 'org.junit.jupiter:junit-jupiter-engine:5.4.0'
}

ext {
    ossrhUsername = ossrhUser
    ossrhPassword = ossrhPass
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task testJar(type: Jar) {
    classifier = 'tests'
    from sourceSets.test.output
}

artifacts {
    archives javadocJar, sourcesJar, testJar
}

signing {
    sign configurations.archives
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        packaging 'jar'
        artifactId 'webp4j'
        url 'https://github.com/zakgof/webp4j'
        
        name 'webp4j'
        description 'Simple Java webp image format encoder and decoder'

        scm {
          connection 'scm:git:https://github.com/zakgof/webp4j.git'
          developerConnection 'scm:git:https://github.com/zakgof/webp4j.git'
          url 'https://github.com/zakgof/webp4j'
        }

        licenses {
          license {
            name 'The Apache License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }

        developers {
          developer {
            id 'zakgof'
            name 'Oleksandr Zakusylo'
            email 'zakgof@gmail.com'
          }
        }
      }
    }
  }
}


