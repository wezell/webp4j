plugins {
    id "java"
    id "maven"
    id "maven-publish"
    id "com.jfrog.bintray" version "1.8.4"
}

compileJava.options.encoding = 'UTF-8'

group = 'com.github.zakgof'
archivesBaseName = 'webp4j'
version = '0.0.1'

ext {
   descr = 'Simple Java lib for encoding/decoding WebP image format'
}

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile 'org.scijava:native-lib-loader:2.3.2'
    testCompile 'org.junit.jupiter:junit-jupiter-engine:5.4.0'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.java
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

def pomConfig = {
     scm {
          connection 'scm:git:https://github.com/zakgof/webp4j.git'
          developerConnection 'scm:git:https://github.com/zakgof/webp4j.git'
          url 'https://github.com/zakgof/webp4j'
        }

        licenses {
          license {
            name 'The Apache License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }

        developers {
          developer {
            id 'zakgof'
            name 'Oleksandr Zakusylo'
            email 'zakgof@gmail.com'
          }
        }
}

publishing {
	publications {
		MyPublication(MavenPublication) {
			from components.java
			artifact sourcesJar
			artifact javadocJar
  		    groupId group  		    
            artifactId archivesBaseName
            version project.version
            pom.withXml {
                def root = asNode()
                root.appendNode('description', project.descr)
                root.appendNode('name', project.archivesBaseName)
                root.appendNode('url', 'https://github.com/zakgof/webp4j.git')
                root.children().last() + pomConfig
            }
		}
	}
}


bintray {
    user = 'zakgof'
    key = project.hasProperty('bintrayKey') ? bintrayKey : System.getenv('BINTRAY_KEY')
    publications = ['MyPublication']
    configurations = ['archives']
    override = true
    pkg {
        repo = 'maven'
        name = archivesBaseName
        userOrg = 'zakgof'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/zakgof/webp4j.git'
        version {
            name = project.version
        }
    }
}
